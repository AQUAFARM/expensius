/*
 * Copyright (C) 2015 Mantas Varnagiris.
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 */

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'crashlytics'

Properties props = new Properties()
try {
    File propsFile = new File('financius.properties')
    props.load(propsFile.newDataInputStream())
} catch (Exception ignore) {
}

android {
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    compileSdkVersion 22
    buildToolsVersion '23.0.0 rc3'

    defaultConfig {
        applicationId "com.mvcoding.financius"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "ENDPOINT_WEB_CLIENT_ID", "\"" + props.getProperty('endpoint_web_client_id') + "\""
        buildConfigField "String", "LOCAL_SERVER_IP", "\"" + getLocalIp() + "\""
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':core')

    // Play Services
    compile 'com.google.android.gms:play-services-base:7.5.0'
    compile 'com.google.android.gms:play-services-gcm:7.5.0'
    compile 'com.google.android.gms:play-services-identity:7.5.0'
    compile 'com.google.android.gms:play-services-analytics:7.5.0'
    compile 'com.google.android.gms:play-services-maps:7.5.0'
    compile 'com.google.android.gms:play-services-location:7.5.0'

    // Support
    compile 'com.android.support:design:22.2.1'
    compile 'com.android.support:support-annotations:22.2.1'
    compile 'com.android.support:recyclerview-v7:22.2.1'
    compile 'com.android.support:cardview-v7:22.2.1'
    compile 'uk.co.chrisjenx:calligraphy:2.1.0'
    compile 'com.github.bumptech.glide:glide:3.6.0'
    compile 'me.relex:circleindicator:1.1.3@aar'
    compile 'me.grantland:autofittextview:0.2.1'
    compile 'net.danlew:android.joda:2.8.1'

    // RxJava
    compile 'io.reactivex:rxjava:1.0.12'
    compile 'io.reactivex:rxandroid:0.25.0'
    compile 'com.squareup.sqlbrite:sqlbrite:0.2.0'

    // Injection
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.google.dagger:dagger:2.0.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'frankiesardo:icepick:3.0.2'
    apt 'frankiesardo:icepick-processor:3.0.2'
    apt 'com.google.dagger:dagger-compiler:2.0.1'

    // Network
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'

    // Debugging
    compile 'com.crashlytics.android:crashlytics:1.1.13'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-SNAPSHOT'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-SNAPSHOT'

    // Tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.8-beta'
    testCompile('com.squareup.assertj:assertj-android:1.0.0') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    // Lombok
    provided "org.projectlombok:lombok:1.12.6"
    apt "org.projectlombok:lombok:1.12.6"
}

def getLocalIp() {
    return InetAddress.getLocalHost().getHostAddress();
}
